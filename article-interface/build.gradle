apply plugin: 'org.asciidoctor.jvm.convert'
apply plugin: 'com.google.cloud.tools.jib'

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

configurations {
    asciidoctorExtensions
}

dependencies {
    // Web
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Security
//    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Tests & Docs
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'

    // Project
    implementation project(':article-application')
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    configurations 'asciidoctorExtensions'
    inputs.dir snippetsDir
    dependsOn test
}

jib {
    extraDirectories.paths {
        path {
            from = asciidoctor.outputDir
            into = '/app/resources/static/'
        }
    }
}

tasks.jib.dependsOn asciidoctor

springBoot {
    buildInfo()
}
